plugins {
	id 'java'
	id 'eclipse'
	id 'idea'
	id 'maven'
	id 'com.github.johnrengelman.shadow' version '1.2.2'
}

group='io.projuice'
version='0.1-SNAPSHOT'


repositories {
	mavenCentral()
	mavenLocal()
}

def NUBES_VERSION = '0.1-SNAPSHOT'
def VERTX_VERSION = '3.1.0'

dependencies {

	compile "com.github.aesteve:nubes-hibernate:${NUBES_VERSION}" // Nubes as transitive dependency
	compile "io.vertx:vertx-web-templ-handlebars:${VERTX_VERSION}"
	compile "io.vertx:vertx-redis-client:${VERTX_VERSION}"
	compile "com.google.guava:guava:18.0"

	// Handlebars
	runtime 'com.github.jknack:handlebars:2.1.0'
	
	// Databases
	runtime 'com.h2database:h2:1.4.186'

	testCompile "io.vertx:vertx-unit:${VERTX_VERSION}"
	testCompile 'junit:junit:4.12'
}

compileJava {
	options.compilerArgs << '-parameters'
}

shadowJar  {
	classifier = ''
	manifest {
		attributes 'Main-Class': 'io.vertx.core.Starter'
		attributes 'Main-Verticle': 'java:io.projuice.Server'
	}
	mergeServiceFiles {
		include 'META-INF/services/io.vertx.core.spi.VerticleFactory'
	}
	dependencies {
		exclude(dependency('io.vertx:codegen'))
		exclude(dependency('junit:junit'))
		exclude(dependency('org.mvel:mvel2'))
	}
	into 'web', {
		from 'web'
	}
}

task start(dependsOn: shadowJar) << {
	javaexec {
		main="-jar"
		args=[
			shadowJar.archivePath,
			"-conf",
			"conf.json"
		]
	}
}

test.outputs.upToDateWhen { false }

task wrapper(type: Wrapper) {
	gradleVersion = '2.8'
}
